apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

ext.versionMajor = 1
ext.versionMinor = 0
ext.versionPatch = 0
ext.versionCode = 1

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    defaultConfig {
        applicationId "com.pedroabinajm.thebestburgers"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode versionCode
        versionName computeVersionName()
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
}

kapt {
    generateStubs = true
}

dependencies {
    def presentationDependencies = rootProject.ext.presentationDependencies
    def coreTestDependencies = rootProject.ext.coreTestDependencies

    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(':core')
    implementation project(':hamburgers')

    // DI
    kapt presentationDependencies.daggerCompiler
    kapt presentationDependencies.daggerAndroidProcessor
    kaptAndroidTest presentationDependencies.daggerCompiler

    // test dependencies
    testImplementation coreTestDependencies.junit
    testImplementation coreTestDependencies.mockitoKotlin
    testImplementation coreTestDependencies.mockito

    // Android test dependencies
    androidTestImplementation coreTestDependencies.testRunner
    androidTestImplementation coreTestDependencies.testRules
    androidTestImplementation coreTestDependencies.espresso, {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    androidTestImplementation coreTestDependencies.espressoContrib, {
        exclude group: 'com.android.support', module: 'appcompat'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude module: 'recyclerview-v7'
    }
    androidTestImplementation coreTestDependencies.mockitoAndroid
    androidTestImplementation coreTestDependencies.espressoIntents
}

repositories {
    mavenCentral()
}

@SuppressWarnings("GrMethodMayBeStatic")
def computeVersionName() {
    return "${versionMajor}.${versionMinor}.${versionPatch}.${versionCode}." + (System.getenv('BUILD_NUMBER') ?: '0')
}